    plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
}

group 'com.bitvault'
version '1.0'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.9.0'
}

sourceCompatibility = '18'
targetCompatibility = '18'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.bitvault'
    mainClass = 'com.bitvault.Launcher'
}


javafx {
    version = '18.0.1'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {

    // argon2
    implementation group: 'de.mkammerer', name: 'argon2-jvm', version: '2.11'

    //needed for argon2
    implementation group: 'net.java.dev.jna', name: 'jna', version: '5.12.1'

    //json
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.3'

    //icons
    implementation group: 'org.kordamp.ikonli', name: 'ikonli-javafx', version: '12.3.1'
    implementation group: 'org.kordamp.ikonli', name: 'ikonli-materialdesign2-pack', version: '12.3.1'

    //sqlite
    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.39.2.0'

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Main-Class' : 'com.bitvault.Launcher'
    }
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'TheVault'
    }
}

jlinkZip {
    group = 'distribution'
}
jpackage{

}

task dist {
    dependsOn clean, jpackage
    description "Calls clean and then jpackage [default]"
}